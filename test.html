create or replace procedure pr_collect_hol_links as
begin
    for i in (SELECT a.ACCOUNT_NUMBER TECH_LOAN
                   , a.FIELD_CHAR_5 parent_loan
                   , case when BLMV_EVENT_REQUIRED = 'N' and a.PRODUCT_CODE like 'R_H%' then 'H1' -- Transferred interest
                          when BLMV_EVENT_REQUIRED = 'Y' and a.PRODUCT_CODE like 'R_H%' then 'HA' -- Transferred principal
                          when regexp_like(child_product, 'R.O\d') then 'O1'                     -- Transferred overdue interest
                          when regexp_like(child_product, 'R.O\D') then 'OA'                     -- Transferred overdue principal
                     end TECH_LOAN_TYPE
                   , a.branch_code
                FROM fcc.CLTB_ACCOUNT_APPS_MASTER a
                join fcc.CLTM_CHILD_LOAN_PRODUCT c on a.PRODUCT_CODE = c.child_product
                WHERE 1=1
                 AND regexp_like(a.FIELD_CHAR_5, '\d{8}\w{3}\w{4}\d{3}')
                 AND (a.PRODUCT_CODE like 'R_H%' or a.PRODUCT_CODE like 'R_O%')
                 AND account_status <> 'V'
                 AND not exists (select * from CLTB_HOL_TECH_LOANS_LINK l where l.tech_loan = a.ACCOUNT_NUMBER))
    loop
            declare
                v_res CLTB_HOL_TECH_LOANS_LINK%rowtype;
            begin
                v_res.PARENT_LOAN    := i.parent_loan;
                v_res.TECH_LOAN_TYPE := i.TECH_LOAN_TYPE;
                v_res.TECH_LOAN      := i.TECH_LOAN;
                v_res.CREATION_TIME  := sysdate;

                select max(st.restruct_event_dt) hol_start
                  into v_res.HOL_START
                  from fcc.CLTB_LOAN_RESTRUCT_EVENT st
                 where 1=1
                   and st.account_number = i.parent_loan
                   and st.branch_code = i.branch_code
                   and st.restruct_type in ('R41', 'R43') -- Mrtg Holidays completed, Credit Holidays COVID-19 completed
                   and st.restruct_event = 'E12' -- Start date
                   and not exists -- Without rollback
                             (select *
                                from fcc.CLTB_LOAN_RESTRUCT_EVENT rl
                               where st.account_number = rl.account_number
                                 and st.branch_code = rl.branch_code
                                 and st.restruct_type = rl.restruct_type       -- Holiday - Rollback - Holiday, 24.11.20
                                 and st.restruct_type_dt = rl.restruct_type_dt -- Holiday - Rollback - Holiday, 24.11.20
                                 and rl.restruct_event in ('E14'  -- Rollback
                                                          ,'E15'))-- Rollback 120
                     ;
                if v_res.HOL_START is null then
                    fcc.dbg.error('No holidays. TECH_LOAN='||v_res.TECH_LOAN||', PARENT_LOAN='||v_res.PARENT_LOAN, 'fccucb.pr_collect_hol_links');
                else
                    insert into CLTB_HOL_TECH_LOANS_LINK values v_res;
                end if;
            exception
                when others then
                    fcc.dbg.error(SQLCODE || ' ' || SQLERRM || '. TECH_LOAN='||v_res.TECH_LOAN||', PARENT_LOAN='||v_res.PARENT_LOAN, 'fccucb.pr_collect_hol_links');
            end;
    end loop;
    commit;
end;
