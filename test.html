CREATE OR REPLACE PACKAGE BODY FCCUCB.BO_REPORTS_CL
AS
 /*------------------------------------------------------------------------------------------------------
  CHANGE HISTORY

  Changed By         : Dmitry Shatskiy, 03/03/2023
  Search Line        : #FCJ-677_lh_schedules_new
  Change Description : Move schedules source from clpks_bo_credit_holiday to lh_schedules_new

-------------------------------------------------------------------------------------------------------*/
  --
  FUNCTION Is_working_day ( P_Date  IN  DATE,
                        P_Banch   IN  VARCHAR2 )
    RETURN NUMBER   --  1 - working day; 0 - weekend; -1 - else
    IS
        CURSOR curDayWH
            IS
            SELECT SUBSTR (holiday_list, CAST (TO_CHAR (P_Date, 'DD') AS NUMBER), 1)
                FROM fcc.STTM_LCL_HOLIDAY
                WHERE   BRANCH_CODE = P_Banch
                    AND MONTH       = TO_CHAR (P_Date, 'MM')
                    AND YEAR        = TO_CHAR (P_Date, 'YYYY');
        cWH     VARCHAR2 (1);
    BEGIN
        OPEN  curDayWH;
        FETCH curDayWH INTO cWH;
        CLOSE curDayWH;

        RETURN  CASE cWH
                    WHEN 'W' THEN 1
                    WHEN 'H' THEN 0
                    ELSE -1
                END;
  END Is_working_day;
  --
  --
  FUNCTION Get_current_date (p_Account_number  IN  VARCHAR2)
    RETURN DATE
   IS
    CURSOR curTodayAcc
        IS
        SELECT d.TODAY
          FROM FCC.CLTB_ACCOUNT_APPS_MASTER M,
               FCC.STTM_DATES D
          WHERE   d.BRANCH_CODE  = m.BRANCH_CODE
            AND ACCOUNT_NUMBER = p_Account_number;
     dCurDate    DATE;
  BEGIN
     OPEN  curTodayAcc;
     FETCH curTodayAcc INTO dCurDate;
     CLOSE curTodayAcc;
     RETURN dCurDate;
  END Get_current_date;
  FUNCTION Get_basis_type (P_ACCOUNT_NUMBER IN VARCHAR2) RETURN VARCHAR2 IS
    lv_field_no NUMBER;
    lv_penalty_rate_type VARCHAR2(30);
  BEGIN
    SELECT field_no into lv_field_no FROM FCC.CLTM_PRODUCT_UDF
     WHERE field_name ='PENAL_RATE_BASIS'
       AND product_code=(SELECT Product_code
                           FROM FCC.CLTB_ACCOUNT_MASTER
                          WHERE ACCOUNT_NUMBER = P_ACCOUNT_NUMBER);
    IF lv_field_no is not null then
      execute immediate
        'SELECT field_char_'||to_char(lv_field_no)||' FROM FCC.CLTB_ACCOUNT_APPS_MASTER
          WHERE account_number='''||P_ACCOUNT_NUMBER||'''' into lv_penalty_rate_type;
    END IF;
    RETURN NVL(lv_penalty_rate_type, 'ANNUAL');
  EXCEPTION WHEN OTHERS THEN
    return 'ANNUAL';
  END Get_basis_type;
  --
  FUNCTION Get_cl_current_due(P_ACCOUNT_NUMBER IN VARCHAR2)
    RETURN FCCUCB.CLPKS_BO_CREDIT_HOLIDAY.table_cl_curr_due_amt PIPELINED IS
  BEGIN
    PIPE ROW(FCCUCB.CLPKS_BO_CREDIT_HOLIDAY.get_cl_current_due_rec(P_ACCOUNT_NUMBER));
  END Get_cl_current_due;
  --
  -- Функция расчета
  FUNCTION Get_calc_amount(p_in_amount  number,
                           p_rate_value number,
                           p_date_from  date,
                           p_date_to    date,
                           p_basis_type  VARCHAR2) return number is
    v_out_calc_amount NUMBER:=0;
    v_count_leap_days NUMBER;
    v_count_ordn_days NUMBER;
  begin
    if mod (extract (year from p_date_to), 4) = 0 and mod (extract (year from p_date_from), 4) != 0
    then
      v_count_leap_days := p_date_to - (trunc (p_date_to, 'rrrr')-1);
      v_count_ordn_days := (trunc (p_date_to, 'rrrr')-1) - p_date_from;
    elsif mod (extract (year from p_date_to), 4) != 0 and mod (extract (year from p_date_from), 4) = 0
     then
      v_count_leap_days := (trunc (p_date_to, 'rrrr')-1) - p_date_from;
      v_count_ordn_days := p_date_to - (trunc (p_date_to, 'rrrr')-1);
    elsif mod (extract (year from p_date_to), 4) = 0 and mod (extract (year from p_date_from), 4) = 0
     then
      v_count_leap_days := p_date_to - p_date_from;
      v_count_ordn_days := 0;
    else
      v_count_leap_days := 0;
      v_count_ordn_days := p_date_to - p_date_from;
    end if;
    IF p_basis_type = 'ANNUAL' THEN
      v_out_calc_amount := round(nvl(p_in_amount,0)*(p_rate_value/100)*( v_count_leap_days / 366 + v_count_ordn_days / 365 ),2);
    ELSE
      v_out_calc_amount:=round(nvl(p_in_amount,0)*(p_rate_value/100)*(p_date_to-p_date_from),2);
    END IF;
    return NVL(v_out_calc_amount,0);
  EXCEPTION WHEN OTHERS THEN
    return 0;
  end Get_calc_amount;
  --
  FUNCTION Get_rate_value(p_account_number  VARCHAR2,
                          p_rate_type       VARCHAR2,
                          p_rate_date       DATE)
  RETURN number is
    v_rate_value  NUMBER;
  begin
    SELECT MAX (UDE_VALUE)
           KEEP (DENSE_RANK LAST ORDER BY EFFECTIVE_DATE)
      INTO v_rate_value
      FROM fcc.CLTB_ACCOUNT_UDE_VALUES
     WHERE ACCOUNT_NUMBER  = p_account_number
       AND UDE_ID          = UPPER (p_rate_type)
       AND EFFECTIVE_DATE <= p_rate_date;

        RETURN NVL (v_rate_value, 0);
  end Get_rate_value;
  --
  FUNCTION Calc_amount_for_period(p_account_number varchar2,
                                  p_in_amount  number,
                                  p_rate_type  VARCHAR2,
                                  p_basis_type VARCHAR2,
                                  p_date_from  date,
                                  p_date_to    date) return number is
    v_out_calc_amount NUMBER:=0;
    TYPE CALC_dates_type IS TABLE OF DATE INDEX BY BINARY_INTEGER;
  CALC_dates_TBL CALC_dates_type;
  begin
    WITH CALC_PERIOD AS (select effective_date-1 as dt
                          from fcc.cltb_account_ude_values
                         where account_number = p_account_number
                           and effective_date >= p_date_from
                           and effective_date <= p_date_to
                           and ude_id = UPPER (p_rate_type)
                         UNION ALL
                          select p_date_from as dt from dual
                         UNION ALL
                          select p_date_to as dt from dual
                        )
    SELECT DISTINCT dt BULK COLLECT into CALC_dates_TBL FROM CALC_PERIOD  ORDER BY dt;
    --
    FOR i IN 1..CALC_dates_TBL.COUNT-1 LOOP
      v_out_calc_amount := v_out_calc_amount + Get_calc_amount(p_in_amount,
                                                               Get_rate_value (P_ACCOUNT_NUMBER, p_rate_type, CALC_dates_TBL(i+1)),
                                                               CALC_dates_TBL(i), CALC_dates_TBL(i+1), p_basis_type);
    END LOOP;
    return v_out_calc_amount;
  EXCEPTION WHEN OTHERS THEN
    return 0;
  end Calc_amount_for_period;

  PROCEDURE Calc_interest_amount_on_date(P_rec_due_amt_curr IN FCCUCB.CLPKS_BO_CREDIT_HOLIDAY.row_cl_curr_due_amt, P_principal_chg_today IN NUMBER, P_curr_date IN DATE, P_pdp_date IN DATE,
                                          P_rec_due_amt_on_date OUT FCCUCB.CLPKS_BO_CREDIT_HOLIDAY.row_cl_curr_due_amt)
  IS
  BEGIN
    P_rec_due_amt_on_date.PRINCIPAL := P_rec_due_amt_curr.PRINCIPAL;
    P_rec_due_amt_on_date.HOL_PRI  := P_rec_due_amt_curr.HOL_PRI;
    P_rec_due_amt_on_date.INTEREST  := P_rec_due_amt_curr.INTEREST
                                           + Get_calc_amount(P_rec_due_amt_curr.PRINCIPAL+P_principal_chg_today,
                                                             Get_rate_value(P_rec_due_amt_curr.ACCOUNT_NUMBER, 'INTEREST_RATE', P_curr_date),
                                                             P_curr_date-1, P_curr_date, 'ANNUAL')
                                           + Calc_amount_for_period(P_rec_due_amt_curr.ACCOUNT_NUMBER, P_rec_due_amt_curr.PRINCIPAL, 'INTEREST_RATE','ANNUAL',
                                                                    P_curr_date, P_pdp_date);
    P_rec_due_amt_on_date.HOL_INT   := P_rec_due_amt_curr.HOL_INT;
  END Calc_interest_amount_on_date;
  --
  FUNCTION Calc_due_amount_on_date_rec(P_ACCOUNT_NUMBER VARCHAR2, P_holiday_type VARCHAR2, P_curr_date DATE, P_pdp_date DATE,
                                       P_ovd_date DATE, P_hol_end_date DATE)
    RETURN FCCUCB.CLPKS_BO_CREDIT_HOLIDAY.row_cl_curr_due_amt
  IS
    rec_due_amt_on_date FCCUCB.CLPKS_BO_CREDIT_HOLIDAY.row_cl_curr_due_amt; 
    rec_cl_curr_due_amt FCCUCB.CLPKS_BO_CREDIT_HOLIDAY.row_cl_curr_due_amt;
    v_tech_payment      NUMBER(3);
    v_Prnl_chg_today    NUMBER :=0;
    v_Prnl_incr_today   NUMBER :=0;
    v_sum_int_tech_pmnt NUMBER :=0;
    v_principal_after_tech_pmnt NUMBER :=0;
    v_max_tech_pmnt_date DATE;
    v_int_tech_pmnt_date DATE;
  BEGIN
    --Amount of principal  prepaid or restored on the current day
    SELECT NVL(sum(AMOUNT_PAID),0) AMOUNT_PAID
      INTO v_Prnl_chg_today
      FROM FCC.CLTB_AMOUNT_PAID t
     WHERE t.ACCOUNT_NUMBER = P_ACCOUNT_NUMBER
       AND PAID_DATE <= DUE_DATE
       AND COMPONENT_NAME = 'PRINCIPAL'
       AND EXEC_DATE = P_curr_date;
    -- Amount of principal increased or restored on the current day
    SELECT NVL(SUM(AMOUNT),0) AMOUNT
      INTO v_Prnl_incr_today
      FROM fcc.cltb_event_entries
     WHERE ACCOUNT_NUMBER = P_ACCOUNT_NUMBER
       AND AMOUNT_TAG = 'PRINCIPAL_INCR'
       AND VALUE_DATE = P_curr_date;
    -- Get current due amounts (prev.date)
    SELECT t.branch_code,
           P_ACCOUNT_NUMBER,
           t.INTEREST,
           t.PRINCIPAL,
           t.OVDI,
           t.OVDP,
           t.PENI,
           t.PENP,
           t.INT_OVDP,
           t.HOL_INT,
           t.HOL_PRI,
           t.HOL_OVDI,
           t.HOL_OVDP,
           t.HOL_PENI,
           t.HOL_PENP,
           t.HOL_INT_OVDP
      INTO rec_cl_curr_due_amt.branch_code,
           rec_cl_curr_due_amt.ACCOUNT_NUMBER,
           rec_cl_curr_due_amt.INTEREST,
           rec_cl_curr_due_amt.PRINCIPAL,
           rec_cl_curr_due_amt.OVDI,
           rec_cl_curr_due_amt.OVDP,
           rec_cl_curr_due_amt.PENI,
           rec_cl_curr_due_amt.PENP,
           rec_cl_curr_due_amt.INT_OVDP,
           rec_cl_curr_due_amt.HOL_INT,
           rec_cl_curr_due_amt.HOL_PRI,
           rec_cl_curr_due_amt.HOL_OVDI,
           rec_cl_curr_due_amt.HOL_OVDP,
           rec_cl_curr_due_amt.HOL_PENI,
           rec_cl_curr_due_amt.HOL_PENP,
           rec_cl_curr_due_amt.HOL_INT_OVDP
      FROM TABLE (FCCUCB.clpks_bo_credit_holiday.GET_CL_CURRENT_DUE(P_ACCOUNT_NUMBER)) t;
    -- HOLIDAY TYPE = M76, M106
    IF P_holiday_type = 'M' THEN
      -- CALC_PAR_M_I_BEFORE_M
      IF (P_pdp_date<=P_hol_end_date OR (P_pdp_date>=P_hol_end_date AND P_curr_date<P_hol_end_date))  THEN
        select MAX(SCHEDULE_DUE_DATE)
          into v_max_tech_pmnt_date
          from FCC.CLTB_ACCOUNT_SCHEDULES t
         where t.ACCOUNT_NUMBER = rec_cl_curr_due_amt.ACCOUNT_NUMBER
           and (t.SCHEDULE_DUE_DATE >= P_curr_date and t.SCHEDULE_DUE_DATE <= P_pdp_date)
           and component_name = 'PRINCIPAL';
        -- If technical payments in Credit Holiday Period exists
        IF v_max_tech_pmnt_date IS NOT NULL THEN
          select NVL(SUM(NVL(amount_due,0) - NVL(amount_Settled,0)),0)
            into v_principal_after_tech_pmnt
            from fcc.cltb_account_schedules t
           where t.ACCOUNT_NUMBER = rec_cl_curr_due_amt.ACCOUNT_NUMBER
             and t.COMPONENT_NAME = 'PRINCIPAL'
             and (t.SCHEDULE_DUE_DATE > v_max_tech_pmnt_date);
          --
          select NVL(SUM(NVL(amount_due,0) - NVL(amount_Settled,0)),0)
            into v_sum_int_tech_pmnt
            from fcc.cltb_account_schedules t
           where t.ACCOUNT_NUMBER = rec_cl_curr_due_amt.ACCOUNT_NUMBER
             and t.COMPONENT_NAME = 'MAIN_INT'
             and (t.SCHEDULE_DUE_DATE >= P_curr_date and t.SCHEDULE_DUE_DATE <= v_max_tech_pmnt_date);
          --
          rec_due_amt_on_date.PRINCIPAL := rec_cl_curr_due_amt.PRINCIPAL;
          rec_due_amt_on_date.HOL_PRI  := rec_cl_curr_due_amt.HOL_PRI;
          rec_due_amt_on_date.INTEREST  := v_sum_int_tech_pmnt
                                                + Calc_amount_for_period(rec_cl_curr_due_amt.ACCOUNT_NUMBER, v_principal_after_tech_pmnt,
                                                                         'INTEREST_RATE','ANNUAL',
                                                                         v_max_tech_pmnt_date, P_pdp_date);
          rec_due_amt_on_date.HOL_INT   := rec_cl_curr_due_amt.HOL_INT;
        ELSE
          Calc_interest_amount_on_date(rec_cl_curr_due_amt, v_Prnl_chg_today, P_curr_date, p_pdp_date, rec_due_amt_on_date);
        END IF;
      ELSE --CALC_PAR_M_I_AFTER_M
        Calc_interest_amount_on_date(rec_cl_curr_due_amt, 0, P_curr_date, p_pdp_date, rec_due_amt_on_date);
      END IF;
    -- HOLIDAY TYPE = С106
    ELSIF P_holiday_type = 'C' THEN
      -- Get date of technical interest payment
      v_int_tech_pmnt_date := P_hol_end_date;
      LOOP
        EXIT WHEN is_working_day( v_int_tech_pmnt_date,rec_cl_curr_due_amt.branch_code) = 1;
        v_int_tech_pmnt_date := v_int_tech_pmnt_date+1;
      END LOOP;
      --CALC_PAR_M_I_BEFORE_C
      IF P_pdp_date <= P_hol_end_date THEN
        --PDP date coincides with the date of the interest technical payment
        IF (P_pdp_date = P_hol_end_date AND P_hol_end_date = v_int_tech_pmnt_date) THEN
          rec_due_amt_on_date.PRINCIPAL := rec_cl_curr_due_amt.PRINCIPAL;
          rec_due_amt_on_date.HOL_PRI  := rec_cl_curr_due_amt.HOL_PRI;
          rec_due_amt_on_date.HOL_INT   := rec_cl_curr_due_amt.HOL_INT;
          --
          SELECT  NVL(NVL(amount_due,0) - NVL(amount_Settled,0),0)
            INTO   rec_due_amt_on_date.INTEREST
            FROM fcc.cltb_account_schedules t
           WHERE t.ACCOUNT_NUMBER = rec_cl_curr_due_amt.ACCOUNT_NUMBER
             AND t.COMPONENT_NAME = 'MAIN_INT'
             AND schedule_due_date = v_int_tech_pmnt_date;
        ELSE
          Calc_interest_amount_on_date(rec_cl_curr_due_amt, v_Prnl_chg_today, P_curr_date, p_pdp_date, rec_due_amt_on_date);
        END IF;
      ELSE
        --CALC_PAR_M_I_AFTER_C
        IF v_int_tech_pmnt_date>= p_pdp_date AND v_int_tech_pmnt_date > P_hol_end_date THEN
          --
          rec_due_amt_on_date.PRINCIPAL := rec_cl_curr_due_amt.PRINCIPAL;
          rec_due_amt_on_date.HOL_PRI  := rec_cl_curr_due_amt.HOL_PRI;
          rec_due_amt_on_date.HOL_INT   := rec_cl_curr_due_amt.HOL_INT;
          --
          SELECT  NVL(NVL(amount_due,0) - NVL(amount_Settled,0),0)
            INTO   rec_due_amt_on_date.INTEREST
            FROM fcc.cltb_account_schedules t
           WHERE t.ACCOUNT_NUMBER = rec_cl_curr_due_amt.ACCOUNT_NUMBER
             AND t.COMPONENT_NAME = 'MAIN_INT'
             AND schedule_due_date = v_int_tech_pmnt_date;
          rec_due_amt_on_date.INTEREST := rec_due_amt_on_date.INTEREST + Calc_amount_for_period(rec_cl_curr_due_amt.ACCOUNT_NUMBER, rec_due_amt_on_date.PRINCIPAL,
                                                                                                         'INTEREST_RATE','ANNUAL',
                                                                                                         v_int_tech_pmnt_date, P_pdp_date);
        ELSE
          Calc_interest_amount_on_date(rec_cl_curr_due_amt, v_Prnl_chg_today-v_Prnl_incr_today, P_curr_date, p_pdp_date, rec_due_amt_on_date);
        END IF;
      END IF;
    -- NO CREDIT HOLIDAY (and never was)
    ELSE
      Calc_interest_amount_on_date(rec_cl_curr_due_amt, v_Prnl_chg_today-v_Prnl_incr_today, P_curr_date, p_pdp_date, rec_due_amt_on_date);
    END IF;
    -- Penalty and Int_on_ovd_P Calculation
    rec_due_amt_on_date.OVDP := rec_cl_curr_due_amt.OVDP;
    rec_due_amt_on_date.HOL_OVDP  := rec_cl_curr_due_amt.HOL_OVDP;
    rec_due_amt_on_date.OVDI := rec_cl_curr_due_amt.OVDI;
    rec_due_amt_on_date.HOL_OVDI  := rec_cl_curr_due_amt.HOL_OVDI;
    rec_due_amt_on_date.PENI := rec_cl_curr_due_amt.PENI + NVL(Calc_amount_for_period(rec_cl_curr_due_amt.ACCOUNT_NUMBER, rec_cl_curr_due_amt.OVDI, 'PENALTY_RATE',
                                                                                              Get_basis_type(rec_cl_curr_due_amt.ACCOUNT_NUMBER),
                                                                                              P_curr_date, P_ovd_date),0);
    rec_due_amt_on_date.HOL_PENI  := rec_cl_curr_due_amt.HOL_PENI + NVL(Calc_amount_for_period(rec_cl_curr_due_amt.ACCOUNT_NUMBER, rec_cl_curr_due_amt.hol_OVDI, 'PENALTY_RATE',
                                                                                             Get_basis_type(rec_cl_curr_due_amt.ACCOUNT_NUMBER),
                                                                                             P_curr_date, P_ovd_date),0);
    rec_due_amt_on_date.PENP := rec_cl_curr_due_amt.PENP + NVL(Calc_amount_for_period(rec_cl_curr_due_amt.ACCOUNT_NUMBER, rec_cl_curr_due_amt.OVDP, 'PENALTY_RATE',
                                                                                              Get_basis_type(rec_cl_curr_due_amt.ACCOUNT_NUMBER),
                                                                                              P_curr_date, P_ovd_date),0);
    rec_due_amt_on_date.HOL_PENP := rec_cl_curr_due_amt.HOL_PENP + NVL(Calc_amount_for_period(rec_cl_curr_due_amt.ACCOUNT_NUMBER, rec_cl_curr_due_amt.HOL_OVDP, 'PENALTY_RATE',
                                                                                             Get_basis_type(rec_cl_curr_due_amt.ACCOUNT_NUMBER),
                                                                                             P_curr_date, P_ovd_date),0);
    -- Int_on_ovd_P Calculation
    rec_due_amt_on_date.INT_OVDP  := rec_cl_curr_due_amt.INT_OVDP + Calc_amount_for_period(rec_cl_curr_due_amt.ACCOUNT_NUMBER, rec_cl_curr_due_amt.OVDP,
                                                                                                   'INT_OVD_RATE', 'ANNUAL', P_curr_date-1, P_ovd_date);
    rec_due_amt_on_date.HOL_INT_OVDP   := rec_cl_curr_due_amt.HOL_INT_OVDP + Calc_amount_for_period(rec_cl_curr_due_amt.ACCOUNT_NUMBER, rec_cl_curr_due_amt.HOL_OVDP,
                                                                                                  'INT_OVD_RATE', 'ANNUAL', P_curr_date-1, P_ovd_date);
    --
    rec_due_amt_on_date.branch_code    := rec_cl_curr_due_amt.branch_code;
    rec_due_amt_on_date.ACCOUNT_NUMBER := rec_cl_curr_due_amt.ACCOUNT_NUMBER;
    RETURN rec_due_amt_on_date;
  END Calc_due_amount_on_date_rec;
    --
  FUNCTION Calc_due_amount_on_date(P_ACCOUNT_NUMBER VARCHAR2, P_holiday_type VARCHAR2, P_curr_date DATE, P_pdp_date DATE, P_ovd_date DATE,
                                   P_hol_end_date DATE)
    RETURN   FCCUCB.CLPKS_BO_CREDIT_HOLIDAY.table_cl_curr_due_amt PIPELINED IS
  BEGIN
    PIPE ROW(Calc_due_amount_on_date_rec(P_ACCOUNT_NUMBER, P_holiday_type, P_curr_date, P_pdp_date, P_ovd_date, P_hol_end_date));
  END Calc_due_amount_on_date;
  PROCEDURE Get_loan_params (p_loan_account IN VARCHAR2, p_branch OUT VARCHAR2, p_customer_name OUT VARCHAR2, p_maturity_date OUT DATE)
  IS
  BEGIN
    select name_rus3 || ' ' || name_rus1 || ' ' || name_rus2 as customer_name
      into p_customer_name
      from fcc.sttm_cust_personal
     where customer_no = (select customer_id
                           from fcc.cltb_account_master
                          where account_number = p_loan_account
                            and rownum = 1);
    select t.BRANCH_CODE, t.MATURITY_DATE
      into p_branch, p_maturity_date
      from FCC.CLTB_ACCOUNT_MASTER t
     where t.ACCOUNT_NUMBER =  p_loan_account;
  END Get_loan_params;
  --
  FUNCTION Get_current_schedule_2901 (P_ACCOUNT_NUMBER IN varchar2) return table_cur_schedule
    pipelined is
    out_record         row_cur_schedule;
    row_num            number :=0;
    v_holiday_details  CLPKS_HOLIDAY_DX_CUSTOM.t_rec_holiday_details;
    v_err_message      VARCHAR2(1000);
    v_event_E20_date   DATE;
  BEGIN
    
    BEGIN
      select *
        into v_holiday_details
        from (table(CLPKS_HOLIDAY_DX_CUSTOM.fn_get_holiday_details(P_ACCOUNT_NUMBER)));
    EXCEPTION
      when NO_DATA_FOUND then
        null; -- No loan holidays
    END;
    -- FCJ-684
    IF fccadpro.flexadp_esbadp.is_loan_hol_E20_only (P_ACCOUNT_NUMBER) = 'Y' THEN
       select e.RESTRUCT_EVENT_DT  
       into v_event_E20_date
       from fcc.CLTB_LOAN_RESTRUCT_EVENT e
       where e.Account_Number = P_ACCOUNT_NUMBER
              --   and e.Branch_Code = v_brn
                 and e.Restruct_Type in ('R41', 'R43')
                 and e.Restruct_Event = 'E20'
                 and rownum = 1;
    END IF;
    
    IF fccadpro.flexadp_esbadp.is_loan_hol_pending(P_ACCOUNT_NUMBER) = 'Y' THEN
      out_record.REC_NUM      := row_num+1;
      out_record.ERR_COMMENT  := 'По КД '||P_ACCOUNT_NUMBER||' идет оформление кредитных каникул. Информация отсутствует.';
      out_record.PAYMENT_DATE := NULL;
      out_record.EMI_AMOUNT   := NULL;
      out_record.PRINCIPAL    := NULL;
      out_record.INTEREST     := NULL;
      out_record.EARLY_REPAY  := NULL;
      out_record.PRPL_EXPECTED := NULL;
      out_record.PRPL_EXP_FCC := NULL;
      PIPE ROW(out_record);
    ELSE
      IF v_holiday_details.status = 'completed' THEN
        FOR rec in (SELECT *
                      FROM TABLE (fccucb.lh_schedules_new.fn_get_schedules(P_ACCOUNT_NUMBER)) -- #FCJ-677_lh_schedules_new
                 )
        LOOP
          out_record.REC_NUM := row_num+1;
          out_record.PAYMENT_DATE := rec.PAYMENT_DATE;
          out_record.EMI_AMOUNT := rec.EMI_AMOUNT;
          out_record.PRINCIPAL := rec.PRINCIPAL;
          out_record.INTEREST := rec.INTEREST;
          out_record.EARLY_REPAY := nvl(rec.EARLY_PRINCIPAL, 0) + nvl(rec.EARLY_INTEREST, 0);
          out_record.PRPL_EXPECTED := rec.PRPL_EXPECTED;
          out_record.PRPL_EXP_FCC := rec.PRPL_EXPECTED;
          out_record.ERR_COMMENT := rec.ERR_TEXT;
          row_num:=out_record.REC_NUM;
          IF v_event_E20_date IS NOT NULL and out_record.PAYMENT_DATE >= v_event_E20_date THEN
             out_record.EMI_AMOUNT   := NULL;
             out_record.PRINCIPAL    := NULL;
             out_record.INTEREST     := NULL;
             out_record.EARLY_REPAY  := NULL;
             out_record.ERR_COMMENT := 'E20;'||to_char(v_event_E20_date,'dd.mm.yyyy');
             PIPE ROW (out_record);
             EXIT;
          END IF;
          --
          PIPE ROW (out_record);
        END LOOP;
      ELSE
        FOR rec in (SELECT *
                      from TABLE (bodev.BO_HELIOS_353_NEW_PKG.CASHFLOW_F(P_ACCOUNT_NUMBER))
                   )
        LOOP
          out_record.REC_NUM := rec.REC_NUM;
          out_record.PAYMENT_DATE := rec.PAYMENT_DATE;
          out_record.EMI_AMOUNT := rec.EMI_AMOUNT;
          out_record.PRINCIPAL := rec.PRINCIPAL;
          out_record.INTEREST := rec.INTEREST;
          out_record.EARLY_REPAY := rec.EARLY_REPAY;
          out_record.PRPL_EXPECTED := rec.PRPL_EXPECTED;
          out_record.PRPL_EXP_FCC := rec.PRPL_EXP_FCC;
          out_record.ERR_COMMENT := NULL;

          PIPE ROW (out_record);
        END LOOP;
      END IF;
    END IF;
    RETURN;
  END Get_current_schedule_2901;
  --
  FUNCTION Get_Total_Due_Amount_2834 (P_ACCOUNT_NUMBER IN varchar2) return table_total_due_amount
    pipelined is

    out_record         row_total_due_amount;
    rec FCCUCB.CLPKS_BO_CREDIT_HOLIDAY.row_cl_curr_due_amt;
    v_holiday_details  CLPKS_HOLIDAY_DX_CUSTOM.t_rec_holiday_details;
    v_curr_date DATE;
  BEGIN
    v_curr_date := Get_current_date(P_ACCOUNT_NUMBER);
    BEGIN
      select *
        into v_holiday_details
        from (table(CLPKS_HOLIDAY_DX_CUSTOM.fn_get_holiday_details(P_ACCOUNT_NUMBER)));
    EXCEPTION
      when NO_DATA_FOUND then
        null; -- No loan holidays
    END;
    IF fccadpro.flexadp_esbadp.is_loan_hol_pending(P_ACCOUNT_NUMBER) = 'Y' THEN
      out_record.account_number := P_ACCOUNT_NUMBER;
      out_record.prpl_expected  := NULL;
      out_record.int_expected   := NULL;
      out_record.principal_overdue := NULL;
      out_record.interest_overdue  := NULL;
      out_record.int_on_ovd_p_accrued := NULL;
      out_record.penalty_i      := NULL;
      out_record.penalty_p      := NULL;
      out_record.total          := NULL;
      PIPE ROW(out_record);
    ELSE
      IF v_holiday_details.status = 'completed' and
         (v_holiday_details.res_type = 'R41' or v_holiday_details.product_category = 'MORTGAGE') THEN -- Mortgage
        SELECT *
            INTO rec
            FROM TABLE (Calc_due_amount_on_date(P_ACCOUNT_NUMBER,'M', v_curr_date, v_curr_date, v_curr_date, v_holiday_details.HOL_END));
        --FOR rec in (SELECT *
        --              from TABLE (FCCUCB.clpks_bo_credit_holiday.GET_CL_CURRENT_DUE(P_ACCOUNT_NUMBER))
        --           )
        --LOOP
          out_record.account_number := P_ACCOUNT_NUMBER;
          out_record.prpl_expected  := rec.PRINCIPAL + rec.HOL_PRI;
          out_record.int_expected   := rec.INTEREST+ rec.HOL_INT;
          out_record.principal_overdue := rec.OVDP + rec.HOL_OVDP;
          out_record.interest_overdue  := rec.OVDI + rec.HOL_OVDI;
          out_record.int_on_ovd_p_accrued := rec.INT_OVDP+rec.HOL_INT_OVDP;
          out_record.penalty_i      := rec.PENI+rec.HOL_PENI;
          out_record.penalty_p      := rec.PENP+rec.HOL_PENP;
          out_record.total          := rec.PRINCIPAL + rec.HOL_PRI + rec.INTEREST+ rec.HOL_INT + rec.OVDP + rec.HOL_OVDP + rec.OVDI + rec.HOL_OVDI + rec.INT_OVDP+rec.HOL_INT_OVDP
                                       + rec.PENI+rec.HOL_PENI + rec.PENP+rec.HOL_PENP;

          PIPE ROW (out_record);
       -- END LOOP;
      ELSIF v_holiday_details.status = 'completed' and v_holiday_details.res_type = 'R43' -- Covid-19
      THEN
        SELECT *
            INTO rec
            FROM TABLE (Calc_due_amount_on_date(P_ACCOUNT_NUMBER,'C', v_curr_date, v_curr_date, v_curr_date, v_holiday_details.HOL_END));
--        FOR rec in (SELECT *
--                      FROM TABLE (FCCUCB.clpks_bo_credit_holiday.GET_CL_CURRENT_DUE(P_ACCOUNT_NUMBER))
--                   )
--        LOOP
          out_record.account_number := P_ACCOUNT_NUMBER;
          out_record.prpl_expected  := rec.PRINCIPAL + rec.HOL_PRI;
          out_record.int_expected   := rec.INTEREST+ rec.HOL_INT;
          out_record.principal_overdue := rec.OVDP + rec.HOL_OVDP;
          out_record.interest_overdue  := rec.OVDI + rec.HOL_OVDI;
          out_record.int_on_ovd_p_accrued := rec.INT_OVDP+rec.HOL_INT_OVDP;
          out_record.penalty_i      := rec.PENI+rec.HOL_PENI;
          out_record.penalty_p      := rec.PENP+rec.HOL_PENP;
          out_record.total          := rec.PRINCIPAL + rec.HOL_PRI + rec.INTEREST+ rec.HOL_INT + rec.OVDP + rec.HOL_OVDP + rec.OVDI + rec.HOL_OVDI + rec.INT_OVDP+rec.HOL_INT_OVDP
                                       + rec.PENI+rec.HOL_PENI + rec.PENP+rec.HOL_PENP;

          PIPE ROW (out_record);
        --END LOOP;
      ELSE
        FOR rec in (Select account_number, prpl_expected, int_expected,
                           principal_overdue, interest_overdue, int_on_ovd_p_accrued,
                           penalty_p, penalty_i, total
                      from table (BODEV.rep_int_due_calc_pkg.submit(P_ACCOUNT_NUMBER))
                   )
        LOOP
          out_record.account_number := rec.account_number;
          out_record.prpl_expected  := rec.prpl_expected;
          out_record.int_expected   := rec.int_expected;
          out_record.principal_overdue := rec.principal_overdue;
          out_record.interest_overdue  := rec.interest_overdue;
          out_record.int_on_ovd_p_accrued := rec.int_on_ovd_p_accrued;
          out_record.penalty_i      := rec.penalty_i;
          out_record.penalty_p      := rec.penalty_p;
          out_record.total          := rec.total;

          PIPE ROW (out_record);
        END LOOP;
      END IF;
    END IF;
    RETURN;
  END Get_Total_Due_Amount_2834;
  --
  FUNCTION Get_Detail_Due_Amount_2834(P_ACCOUNT_NUMBER IN varchar2) return table_detail_due_amount
    pipelined is

    out_record         row_detail_due_amount;
    v_holiday_details  CLPKS_HOLIDAY_DX_CUSTOM.t_rec_holiday_details;
    v_max_from_date DATE;
  BEGIN
    BEGIN
      select *
        into v_holiday_details
        from (table(CLPKS_HOLIDAY_DX_CUSTOM.fn_get_holiday_details(P_ACCOUNT_NUMBER)));
    EXCEPTION
      when NO_DATA_FOUND then
        null; -- No loan holidays
    END;
    --
    IF fccadpro.flexadp_esbadp.is_loan_hol_pending(P_ACCOUNT_NUMBER) = 'Y' THEN
      out_record.from_date   := NULL;
      out_record.to_date     := NULL;
      out_record.INT_RATE    := NULL;
      out_record.ACCR_INT    := NULL;
      out_record.INT_EXPECTED  := NULL;
      out_record.INT_PREPAID   := NULL;
      out_record.INT_DUE       := NULL;
      out_record.EMI_AMOUNT    := NULL;
      out_record.PRINCIPAL      := NULL;
      out_record.PRPL_EXPECTED  := NULL;
      out_record.ERR_COMMENT  := 'По КД '||P_ACCOUNT_NUMBER||' идет оформление кредитных каникул. Информация отсутствует.';
      PIPE ROW(out_record);
    ELSE
      IF v_holiday_details.status = 'completed' and
         (v_holiday_details.res_type = 'R41' or v_holiday_details.product_category = 'MORTGAGE') THEN -- Mortgage
        BEGIN
          select max(T.payment_date)
            into v_max_from_date
            from TABLE (FCCUCB.clpks_bo_credit_holiday.get_cl_sch_with_hol(P_ACCOUNT_NUMBER)) T
           WHERE T.payment_date < FCCUCB.clpks_bo_credit_holiday.CUR_DATE (P_ACCOUNT_NUMBER)
         ;
        EXCEPTION
          when NO_DATA_FOUND then
           v_max_from_date := null; -- No loan holidays
        END;
        --
        FOR rec in (SELECT *
                      FROM TABLE (FCCUCB.clpks_bo_credit_holiday.get_cl_sch_with_hol(P_ACCOUNT_NUMBER))
                      WHERE payment_date = v_max_from_date
                   )
        LOOP
          out_record.from_date := v_max_from_date;
          out_record.to_date := FCCUCB.clpks_bo_credit_holiday.CUR_DATE (P_ACCOUNT_NUMBER);
          out_record.INT_RATE := FCCUCB.clpks_bo_credit_holiday.get_ude_val ( v_holiday_details.branch_code, P_ACCOUNT_NUMBER, 'INTEREST_RATE', v_max_from_date);
          out_record.ACCR_INT  := rec.Interest;
          SELECT INT_EXPECTED INTO out_record.INT_EXPECTED
            from TABLE (BO_REPORTS_CL.Get_Total_Due_Amount_2834(P_ACCOUNT_NUMBER)) ;
          out_record.INT_PREPAID := null;
          out_record.INT_DUE  := NULL;--CASE WHEN NVL(out_record.INT_PREPAID,0)=0 THEN rec.Interest ELSE rec.Interest-out_record.INT_EXPECTED END ;
         out_record.EMI_AMOUNT := rec.EMI_AMOUNT;
          out_record.PRINCIPAL  := rec.PRINCIPAL;
          out_record.PRPL_EXPECTED := rec.PRPL_EXPECTED;
          out_record.ERR_COMMENT := rec.ERR_TEXT;

          PIPE ROW (out_record);
        END LOOP;
      ELSIF v_holiday_details.status = 'completed' and v_holiday_details.res_type = 'R43' -- Covid-19
      THEN
        BEGIN
          select max(T.payment_date)
            into v_max_from_date
            from TABLE (FCCUCB.clpks_bo_credit_holiday.get_cl_sch_with_hol2(P_ACCOUNT_NUMBER)) T
           WHERE T.payment_date < FCCUCB.clpks_bo_credit_holiday.CUR_DATE (P_ACCOUNT_NUMBER);
        EXCEPTION
          when NO_DATA_FOUND then
           v_max_from_date := null; -- No loan holidays
        END;
        FOR rec in (SELECT *
                      FROM TABLE (FCCUCB.clpks_bo_credit_holiday.get_cl_sch_with_hol2(P_ACCOUNT_NUMBER))
                      WHERE payment_date = v_max_from_date
                   )
        LOOP
          out_record.from_date := v_max_from_date;
          out_record.to_date := FCCUCB.clpks_bo_credit_holiday.CUR_DATE (P_ACCOUNT_NUMBER);
          out_record.INT_RATE := FCCUCB.clpks_bo_credit_holiday.get_ude_val ( v_holiday_details.branch_code, P_ACCOUNT_NUMBER, 'INTEREST_RATE', v_max_from_date);
          out_record.ACCR_INT  := rec.Interest;
          SELECT INT_EXPECTED INTO out_record.INT_EXPECTED
            from TABLE (BO_REPORTS_CL.Get_Total_Due_Amount_2834(P_ACCOUNT_NUMBER)) ;
          out_record.INT_PREPAID := null;
          out_record.INT_DUE  := null;
          out_record.EMI_AMOUNT := rec.EMI_AMOUNT;
          out_record.PRINCIPAL      := rec.PRINCIPAL;
          out_record.PRPL_EXPECTED      := rec.PRPL_EXPECTED;
          out_record.ERR_COMMENT := rec.ERR_TEXT;

          PIPE ROW (out_record);
        END LOOP;
      ELSE
        FOR rec in (Select *
                      from table (BODEV.rep_int_due_calc_pkg.submit_detal(P_ACCOUNT_NUMBER,'','','submit'))
                     order by from_date)
        LOOP
          out_record.from_date := rec.from_date;
          out_record.to_date := rec.TO_DATE;
          out_record.INT_RATE := rec.INT_RATE;
          out_record.ACCR_INT  := rec.ACCR_INT ;
          out_record.INT_EXPECTED  := rec.INT_EXPECTED ;
          out_record.INT_PREPAID := rec.INT_PREPAID;
          out_record.INT_DUE  := rec.INT_DUE;
          out_record.EMI_AMOUNT := rec.EMI_AMOUNT;
          out_record.PRINCIPAL      := rec.PRINCIPAL;
          out_record.PRPL_EXPECTED      := rec.PRPL_EXPECTED;
          out_record.ERR_COMMENT := NULL;
          --
          PIPE ROW (out_record);
        END LOOP;
      END IF;
    END IF;
    RETURN;
  END Get_Detail_Due_Amount_2834;
  --
  FUNCTION Get_Details_PDP_2842(P_ACCOUNT_NUMBER IN varchar2, P_date_pdp IN DATE, P_date_ovd IN DATE) return table_total_pdp_amount
    pipelined is

    out_record          row_total_pdp_amount;
    v_holiday_details   CLPKS_HOLIDAY_DX_CUSTOM.t_rec_holiday_details;
    rec_due_amt_on_date FCCUCB.CLPKS_BO_CREDIT_HOLIDAY.row_cl_curr_due_amt;
    v_curr_date DATE;
    is_Holiday_exists   BOOLEAN DEFAULT FALSE;
    V_branch            VARCHAR2(3);
    v_customer_name     VARCHAR2(250);
    v_maturity_date     DATE;
    v_next_pmnt_date_afterLH DATE;
  BEGIN
    BEGIN
      select *
        into v_holiday_details
        from (table(CLPKS_HOLIDAY_DX_CUSTOM.fn_get_holiday_details(P_ACCOUNT_NUMBER)));
        is_Holiday_exists := TRUE;
    EXCEPTION
      when NO_DATA_FOUND then
        is_Holiday_exists := FALSE; -- No loan holidays
    END;
    --
    Get_loan_params(P_ACCOUNT_NUMBER, V_branch, v_customer_name, v_maturity_date);
    --
    out_record.account_number   := P_ACCOUNT_NUMBER;
    out_record.prpl_expected    := 0;
    out_record.int_expected     := 0;
    out_record.principal_overdue:= 0;
    out_record.interest_overdue := 0;
    out_record.int_on_ovd_p_accrued  := 0;
    out_record.penalty_p        := 0;
    out_record.penalty_i        := 0;
    out_record.exp_pay_cnt      := 0;
    out_record.customer_name    := v_customer_name;
    IF is_Holiday_exists AND fccadpro.flexadp_esbadp.is_loan_hol_pending(P_ACCOUNT_NUMBER) = 'Y' THEN
      out_record.check_message    := 'По КД '||P_ACCOUNT_NUMBER||' идет оформление кредитных каникул, либо кредитные каникулы оформлены некорректно. Информация отсутствует.';
      PIPE ROW(out_record);
    ELSE
      out_record.account_number   := P_ACCOUNT_NUMBER;
      v_curr_date := Get_current_date(P_ACCOUNT_NUMBER);
      BEGIN
        SELECT MIN(to_date) INTO v_next_pmnt_date_afterLH
          FROM
             (SELECT a.SCHEDULE_ST_DATE from_date, a.SCHEDULE_DUE_DATE to_date, a.amount_due AM_DUE
              FROM fcc.CLTB_ACCOUNT_SCHEDULES  a
               WHERE a.ACCOUNT_NUMBER = P_ACCOUNT_NUMBER
               AND a.COMPONENT_NAME = 'PRINCIPAL'
               AND a.SCHEDULE_DUE_DATE >= greatest(NVL(v_holiday_details.hol_end, v_curr_date), v_curr_date))
       ;
      EXCEPTION WHEN NO_DATA_FOUND THEN
        v_next_pmnt_date_afterLH :=v_maturity_date;
      END;
      --
      IF P_date_pdp > v_next_pmnt_date_afterLH THEN
        out_record.check_message    := 'Дата отчета '||to_char(P_date_pdp, 'dd.mm.yyyy')||' больше даты платежа по гафику '||to_char(v_next_pmnt_date_afterLH, 'dd.mm.yyyy');
        PIPE ROW(out_record);
      ELSIF P_date_pdp < v_curr_date THEN
        out_record.check_message    := 'Дата отчета '||to_char(P_date_pdp, 'dd.mm.yyyy')||' меньше текущей операционной даты '||to_char(v_curr_date, 'dd.mm.yyyy');
        PIPE ROW(out_record);
      ELSE
        --
        IF v_holiday_details.status = 'completed' and
           (v_holiday_details.res_type = 'R41' or v_holiday_details.product_category in ('MORTGAGE', 'MRTG_RSTR_PRIN')) THEN -- Mortgage
          --
          SELECT *
            INTO rec_due_amt_on_date
            FROM TABLE (Calc_due_amount_on_date(P_ACCOUNT_NUMBER,'M', v_curr_date, P_date_PDP, P_date_ovd, v_holiday_details.HOL_END));
        ELSIF v_holiday_details.status = 'completed' and v_holiday_details.res_type = 'R43' -- Covid-19
         THEN
           --
          SELECT *
            INTO rec_due_amt_on_date
            FROM TABLE (Calc_due_amount_on_date(P_ACCOUNT_NUMBER,'C', v_curr_date, P_date_PDP, P_date_ovd, v_holiday_details.HOL_END));
        ELSE
          SELECT *
            INTO rec_due_amt_on_date
            FROM TABLE (Calc_due_amount_on_date(P_ACCOUNT_NUMBER,'', v_curr_date, P_date_PDP, P_date_ovd, NULL));
          --
        END IF;
        out_record.prpl_expected := rec_due_amt_on_date.PRINCIPAL + rec_due_amt_on_date.HOL_PRI;
        out_record.int_expected :=  rec_due_amt_on_date.INTEREST +  rec_due_amt_on_date.HOL_INT;
        out_record.principal_overdue := rec_due_amt_on_date.OVDP+rec_due_amt_on_date.HOL_OVDP;
        out_record.interest_overdue  := rec_due_amt_on_date.OVDI+rec_due_amt_on_date.HOL_OVDI;
        out_record.int_on_ovd_p_accrued  := rec_due_amt_on_date.INT_OVDP + rec_due_amt_on_date.HOL_INT_OVDP;
        out_record.penalty_p := rec_due_amt_on_date.PENP + rec_due_amt_on_date.HOL_PENP;
        out_record.penalty_i  := rec_due_amt_on_date.PENI+rec_due_amt_on_date.HOL_PENI;
        out_record.total := out_record.prpl_expected + out_record.int_expected + out_record.principal_overdue +
                            out_record.interest_overdue + out_record.int_on_ovd_p_accrued + out_record.penalty_p + out_record.penalty_i;
        select count(*)
          into out_record.exp_pay_cnt
         from TABLE (FCCUCB.BO_REPORTS_CL.Get_current_schedule_2901(P_ACCOUNT_NUMBER)) where PAYMENT_DATE>p_date_pdp and NVL(INTEREST,0)>0 ;  
         --
        out_record.customer_name      := v_customer_name;
        out_record.check_message := '';
        PIPE ROW (out_record);
      END IF;
    END IF;
    RETURN;
  END Get_Details_PDP_2842;
-----------------------------------------------------
  FUNCTION GET_REP2377 (pLoan IN VARCHAR2)
        RETURN REP2377_tbl_t
        PIPELINED
    IS
        v_res     LH_REP2361_v6.LOAN_REPAYMENT_ACTUAL_tbl_t;
        res_row   REP2377_row_t;                        -- := REP2377_row_t();
    BEGIN
        SELECT *
          BULK COLLECT INTO v_res
          FROM TABLE (LH_REP2361_v6.GET_LOAN_REPAYMENT_ACTUAL (pLoan))
         WHERE     (NVL(PAID_INTEREST,0) <> 0  AND INTEREST_TECH_FLAG = 0)
              OR (NVL(PAID_PRINCIPAL,0) <> 0 AND PRINCIPAL_TECH_FLAG = 0);

        FOR i IN 1 .. v_res.COUNT
        LOOP
            res_row := NULL;
            res_row.PAID_DATE := v_res (i).PAID_DATE;

            IF v_res (i).PAID_INTEREST IS NOT NULL
            THEN
                res_row.COMPONENT_NAME := 'MAIN_INT';

                IF SUBSTR (pLoan, 9, 3) = 'RUR'
                THEN
                    res_row.LCY_AMOUNT := v_res (i).PAID_INTEREST;
                ELSE
                    res_row.FCY_AMOUNT := v_res (i).PAID_INTEREST;
                END IF;

                PIPE ROW (res_row);
            END IF;

            IF v_res (i).PAID_PRINCIPAL IS NOT NULL
            THEN
                res_row.COMPONENT_NAME := 'PRINCIPAL';

                IF SUBSTR (pLoan, 9, 3) = 'RUR'
                THEN
                    res_row.LCY_AMOUNT := v_res (i).PAID_PRINCIPAL;
                ELSE
                    res_row.FCY_AMOUNT := v_res (i).PAID_PRINCIPAL;
                END IF;

                PIPE ROW (res_row);
            END IF;
        END LOOP;
    END;
    ------------------------------------------------------------
FUNCTION Get_PRINC_MAIN_INT_2377_2480 (p_account_number   IN VARCHAR2,
                       p_beg_date         IN DATE,
                       p_end_date         IN DATE)
    RETURN t_tbl_2377_2480
    PIPELINED
IS
    v_KK            INTEGER;                                           -- shir
    v_row           t_rec_2377_2480;                  --BO_RPT.mort_sch_row_t;
    v_entry_amt     NUMBER;
    v_branch_code   VARCHAR2 (3);
    v_beg_date      DATE := p_beg_date;
    v_end_date      DATE := p_end_date;
BEGIN
    SELECT branch_code
      INTO v_branch_code
      FROM fcc.sttm_sys_acc_master acc
     WHERE acc.base_ref_no = p_account_number AND ROWNUM = 1;

    IF FCCUCB.CLPKS_BO_CREDIT_HOLIDAY.is_credit_holidays (v_branch_code,
                                                          p_account_number)
    --  если есть КК
    THEN
        v_KK := 1;
    ELSE
        v_KK := 0;
    END IF;

    v_row.PRINCIPAL_AMT := NULL;
    v_row.MAIN_INT_AMT := NULL;
-- grant select on FCC.CLVS_ACCOUNT_EVENTS to FCCUCB;
    FOR r_trn_dt
        IN (SELECT account_number, trn_dt
                       FROM FCC.CLVS_ACCOUNT_EVENTS
                      WHERE     account_number = p_account_number
                            AND drcr_ind = 'D'
                            AND gaap_indicator = 'AL'
                            AND event IN ('ALIQ', 'MLIQ')
                            AND component_name IN ('PRINCIPAL', 'MAIN_INT')
                            AND trn_dt BETWEEN v_beg_date AND v_end_date
                            AND v_KK = 0
            UNION ALL
            SELECT DISTINCT p_account_number, paid_date       -- shir 12/10/22
              FROM TABLE (
                       FCCUCB.LH_REP2361_v6.GET_LOAN_REPAYMENT_ACTUAL (
                           p_account_number))
             WHERE     (   (    PAID_PRINCIPAL IS NOT NULL
                            AND PRINCIPAL_TECH_FLAG = 0)
                        OR (    PAID_INTEREST IS NOT NULL
                            AND INTEREST_TECH_FLAG = 0))
                   AND paid_date BETWEEN v_beg_date AND v_end_date
                   AND v_KK = 1
            ORDER BY trn_dt)
    LOOP
        v_row.PAYMENT_DATE := r_trn_dt.trn_dt; -- TO_CHAR (r_trn_dt.trn_dt, 'DD.MM.YYYY');
        v_row.PRINCIPAL_AMT := 0;
        v_row.MAIN_INT_AMT := 0;

        FOR r_entry
            IN (SELECT component_name,
                       amount     entry_amt_fcy,
                       lcy_amount entry_amt_lcy
                  FROM fcc.CLTB_EVENT_ENTRIES a          --CLVS_ACCOUNT_EVENTS
                 WHERE     account_number = p_account_number
                       AND incr_decr_flag = 'D'
                       AND dr_gaap_indicator = 'AL'
                       AND event_code IN ('ALIQ', 'MLIQ')
                       AND component_name IN ('PRINCIPAL', 'MAIN_INT')
                       AND value_date = r_trn_dt.trn_dt
                       AND v_KK = 0
                UNION ALL
                SELECT component_name, fcy_amount, lcy_amount
                  FROM TABLE (
                           FCCUCB.BO_REPORTS_CL.GET_REP2377 (
                               p_account_number))
                 WHERE paid_date = r_trn_dt.trn_dt AND v_KK = 1)
        LOOP
            v_entry_amt := NVL (r_entry.entry_amt_fcy, r_entry.entry_amt_lcy);
            v_entry_amt := TRUNC (v_entry_amt, 2);

            IF r_entry.component_name = 'PRINCIPAL'
            THEN
                v_row.PRINCIPAL_AMT := TO_CHAR (v_entry_amt);
            END IF;

            IF r_entry.component_name = 'MAIN_INT'
            THEN
                v_row.MAIN_INT_AMT := TO_CHAR (v_entry_amt);
            END IF;
        END LOOP;
        v_row.ACCOUNT_NUMBER := p_account_number; 
        PIPE ROW (v_row);
    END LOOP;
END;
END BO_REPORTS_CL;
/
